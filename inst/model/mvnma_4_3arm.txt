model {
  # k = number of studies
  # k2 = number of two-arm studies
  # n = number of treatments
    
  # This controls for studies with one outcome not reported by setting the
  # correlation equal to zero
  #
  for (i in 1:(2 * k - k2)) {
    control12[i] <- step(9999 - var1[i]) * step(9999 - var2[i])
    control13[i] <- step(9999 - var1[i]) * step(9999 - var3[i])
    control14[i] <- step(9999 - var1[i]) * step(9999 - var4[i])
    control23[i] <- step(9999 - var2[i]) * step(9999 - var3[i])
    control24[i] <- step(9999 - var2[i]) * step(9999 - var4[i])
    control34[i] <- step(9999 - var3[i]) * step(9999 - var4[i])
  }
  
  # Two-arm studies
  #
  for (i in 1:k2) {
    #
    # Variances
    #
    S2[i, 1, 1] <- var1[i] + psi1.sq
    S2[i, 2, 2] <- var2[i] + psi2.sq
    S2[i, 3, 3] <- var3[i] + psi3.sq
    S2[i, 4, 4] <- var4[i] + psi4.sq
    #
    # Covariances
    #
    S2[i, 1, 2] <- sqrt(S2[i, 1, 1]) * sqrt(S2[i, 2, 2]) * control12[i] * rho1
    S2[i, 1, 3] <- sqrt(S2[i, 1, 1]) * sqrt(S2[i, 3, 3]) * control13[i] * rho2 
    S2[i, 1, 4] <- sqrt(S2[i, 1, 1]) * sqrt(S2[i, 4, 4]) * control14[i] * rho3
    #
    S2[i, 2, 3] <- sqrt(S2[i, 2, 2]) * sqrt(S2[i, 3, 3]) * control23[i] * rho4
    S2[i, 2, 4] <- sqrt(S2[i, 2, 2]) * sqrt(S2[i, 4, 4]) * control24[i] * rho5
    S2[i, 3, 4] <- sqrt(S2[i, 3, 3]) * sqrt(S2[i, 4, 4]) * control34[i] * rho6
    #
    S2[i, 2, 1] <- S2[i, 1, 2]
    S2[i, 3, 1] <- S2[i, 1, 3]
    S2[i, 4, 1] <- S2[i, 1, 4]
    S2[i, 3, 2] <- S2[i, 2, 3]
    S2[i, 4, 2] <- S2[i, 2, 4]
    S2[i, 4, 3] <- S2[i, 3, 4]
    #
    # Estimates
    #
    y[(4 * i - 3):(4 * i)] ~ dmnorm.vcov(mean[(4 * i - 3):(4 * i)], S2[i, , ])
  }
  
  # Three-arm studies
  #
  for (i in 1:(k - k2)) {
    #
    # Variances
    #
    S3[i, 1, 1] <- var1[k2 + 2 * i - 1] + psi1.sq
    S3[i, 2, 2] <- var2[k2 + 2 * i - 1] + psi2.sq
    S3[i, 3, 3] <- var3[k2 + 2 * i - 1] + psi3.sq
    S3[i, 4, 4] <- var4[k2 + 2 * i - 1] + psi4.sq
    #
    S3[i, 5, 5] <- var1[k2 + 2 * i    ] + psi1.sq
    S3[i, 6, 6] <- var2[k2 + 2 * i    ] + psi2.sq
    S3[i, 7, 7] <- var3[k2 + 2 * i    ] + psi3.sq
    S3[i, 8, 8] <- var4[k2 + 2 * i    ] + psi4.sq
    #
    # Covariances
    #
    S3[i, 1, 2] <- sqrt(S3[i, 1, 1]) * sqrt(S3[i, 2, 2]) * control12[i] * rho1
    S3[i, 1, 3] <- sqrt(S3[i, 1, 1]) * sqrt(S3[i, 3, 3]) * control13[i] * rho2
    S3[i, 1, 4] <- sqrt(S3[i, 1, 1]) * sqrt(S3[i, 4, 4]) * control14[i] * rho3
    S3[i, 2, 3] <- sqrt(S3[i, 2, 2]) * sqrt(S3[i, 3, 3]) * control23[i] * rho4
    S3[i, 2, 4] <- sqrt(S3[i, 2, 2]) * sqrt(S3[i, 4, 4]) * control24[i] * rho5
    S3[i, 3, 4] <- sqrt(S3[i, 3, 3]) * sqrt(S3[i, 4, 4]) * control34[i] * rho6
    #
    S3[i, 5, 6] <- sqrt(S3[i, 5, 5]) * sqrt(S3[i, 6, 6]) * control12[i] * rho1
    S3[i, 5, 7] <- sqrt(S3[i, 5, 5]) * sqrt(S3[i, 7, 7]) * control13[i] * rho2
    S3[i, 5, 8] <- sqrt(S3[i, 5, 5]) * sqrt(S3[i, 8, 8]) * control14[i] * rho3
    S3[i, 6, 7] <- sqrt(S3[i, 6, 6]) * sqrt(S3[i, 7, 7]) * control23[i] * rho4
    S3[i, 6, 8] <- sqrt(S3[i, 6, 6]) * sqrt(S3[i, 8, 8]) * control24[i] * rho5
    S3[i, 7, 8] <- sqrt(S3[i, 7, 7]) * sqrt(S3[i, 8, 8]) * control34[i] * rho6
    #
    S3[i, 1, 5] <- 0.5 * sqrt(S3[i, 1, 1]) * sqrt(S3[i, 5, 5])
    S3[i, 2, 6] <- 0.5 * sqrt(S3[i, 2, 2]) * sqrt(S3[i, 6, 6])
    S3[i, 3, 7] <- 0.5 * sqrt(S3[i, 3, 3]) * sqrt(S3[i, 7, 7])
    S3[i, 4, 8] <- 0.5 * sqrt(S3[i, 4, 4]) * sqrt(S3[i, 8, 8])
    #
    S3[i, 1, 6] <- 0.5 * sqrt(S3[i, 1, 1]) * sqrt(S3[i, 6, 6]) * control12[i] * rho1
    S3[i, 2, 5] <- 0.5 * sqrt(S3[i, 2, 2]) * sqrt(S3[i, 5, 5]) * control12[i] * rho1
    #
    S3[i, 1, 7] <- 0.5 * sqrt(S3[i, 1, 1]) * sqrt(S3[i, 7, 7]) * control13[i] * rho2
    S3[i, 3, 5] <- 0.5 * sqrt(S3[i, 3, 3]) * sqrt(S3[i, 5, 5]) * control13[i] * rho2
    #
    S3[i, 1, 8] <- 0.5 * sqrt(S3[i, 1, 1]) * sqrt(S3[i, 8, 8]) * control14[i] * rho3
    S3[i, 4, 5] <- 0.5 * sqrt(S3[i, 4, 4]) * sqrt(S3[i, 5, 5]) * control14[i] * rho3
    #
    S3[i, 2, 7] <- 0.5 * sqrt(S3[i, 2, 2]) * sqrt(S3[i, 7, 7]) * control23[i] * rho4
    S3[i, 3, 6] <- 0.5 * sqrt(S3[i, 3, 3]) * sqrt(S3[i, 6, 6]) * control23[i] * rho4
    #
    S3[i, 2, 8] <- 0.5 * sqrt(S3[i, 2, 2]) * sqrt(S3[i, 8, 8]) * control24[i] * rho5
    S3[i, 4, 6] <- 0.5 * sqrt(S3[i, 4, 4]) * sqrt(S3[i, 6, 6]) * control24[i] * rho5
    #
    S3[i, 3, 8] <- 0.5 * sqrt(S3[i, 3, 3]) * sqrt(S3[i, 8, 8]) * control34[i] * rho6
    S3[i, 4, 7] <- 0.5 * sqrt(S3[i, 4, 4]) * sqrt(S3[i, 7, 7]) * control34[i] * rho6
    #
    S3[i, 2, 1] <- S3[i, 1, 2]
    S3[i, 3, 1] <- S3[i, 1, 3]
    S3[i, 4, 1] <- S3[i, 1, 4]
    S3[i, 5, 1] <- S3[i, 1, 5]
    S3[i, 6, 1] <- S3[i, 1, 6]
    S3[i, 7, 1] <- S3[i, 1, 7]
    S3[i, 8, 1] <- S3[i, 1, 8]
    S3[i, 3, 2] <- S3[i, 2, 3]
    S3[i, 4, 2] <- S3[i, 2, 4]
    S3[i, 5, 2] <- S3[i, 2, 5]
    S3[i, 6, 2] <- S3[i, 2, 6]
    S3[i, 7, 2] <- S3[i, 2, 7]
    S3[i, 8, 2] <- S3[i, 2, 8]
    S3[i, 4, 3] <- S3[i, 3, 4]
    S3[i, 5, 3] <- S3[i, 3, 5]
    S3[i, 6, 3] <- S3[i, 3, 6]
    S3[i, 7, 3] <- S3[i, 3, 7]
    S3[i, 8, 3] <- S3[i, 3, 8]
    S3[i, 5, 4] <- S3[i, 4, 5]
    S3[i, 6, 4] <- S3[i, 4, 6]
    S3[i, 7, 4] <- S3[i, 4, 7]
    S3[i, 8, 4] <- S3[i, 4, 8]
    S3[i, 6, 5] <- S3[i, 5, 6]
    S3[i, 7, 5] <- S3[i, 5, 7]
    S3[i, 8, 5] <- S3[i, 5, 8]
    S3[i, 7, 6] <- S3[i, 6, 7]
    S3[i, 8, 6] <- S3[i, 6, 8]
    S3[i, 8, 7] <- S3[i, 7, 8]
    #
    # Estimates
    #
    y[(4 * k2 + 8 * i - 7):(4 * k2 + 8 * i)] ~
      dmnorm.vcov(mean[(4 * k2 + 8 * i - 7):(4 * k2 + 8 * i)], S3[i, , ])
  }
  
  # Parameterization of the means
  #
  for (i in 1:k2) {
    mean[4 * i - 3] <- d1[treat2[i]] - d1[treat1[i]]
    mean[4 * i - 2] <- d2[treat2[i]] - d2[treat1[i]]
    mean[4 * i - 1] <- d3[treat2[i]] - d3[treat1[i]]
    mean[4 * i    ] <- d4[treat2[i]] - d4[treat1[i]]
  }
  #
  for (i in 1:(k - k2)) {
    mean[4 * k2 + 8 * i - 7] <- d1[treat2[k2 + i]] - d1[treat1[k2 + i]]
    mean[4 * k2 + 8 * i - 6] <- d2[treat2[k2 + i]] - d2[treat1[k2 + i]]
    mean[4 * k2 + 8 * i - 5] <- d3[treat2[k2 + i]] - d3[treat1[k2 + i]]
    mean[4 * k2 + 8 * i - 4] <- d4[treat2[k2 + i]] - d4[treat1[k2 + i]]
    #
    mean[4 * k2 + 8 * i - 3] <- d1[treat3[k2 + i]] - d1[treat1[k2 + i]]
    mean[4 * k2 + 8 * i - 2] <- d2[treat3[k2 + i]] - d2[treat1[k2 + i]]
    mean[4 * k2 + 8 * i - 1] <- d3[treat3[k2 + i]] - d3[treat1[k2 + i]]
    mean[4 * k2 + 8 * i    ] <- d4[treat3[k2 + i]] - d4[treat1[k2 + i]]
  }
  
  # Priors
  #
  for (i in 1:(ref - 1)) {
    d1[i] ~ dnorm(0, 1e-03)
    d2[i] ~ dnorm(0, 1e-03)
    d3[i] ~ dnorm(0, 1e-03)
    d4[i] ~ dnorm(0, 1e-03)
  }
  #
  d1[ref] <- 0
  d2[ref] <- 0
  d3[ref] <- 0
  d4[ref] <- 0
  #
  for (i in (ref + 1):n) {
    d1[i] ~ dnorm(0, 1e-03)
    d2[i] ~ dnorm(0, 1e-03)
    d3[i] ~ dnorm(0, 1e-03)
    d4[i] ~ dnorm(0, 1e-03)
  }
  #
  psi1.sq <- psi1 * psi1
  psi2.sq <- psi2 * psi2
  psi3.sq <- psi3 * psi3
  psi4.sq <- psi4 * psi4
  #
  psi1 ~ dnorm(0, prec.psi1)T(0, )
  psi2 ~ dnorm(0, prec.psi2)T(0, )
  psi3 ~ dnorm(0, prec.psi3)T(0, )
  psi4 ~ dnorm(0, prec.psi4)T(0, )
  #
  rho1 ~ dunif(lower.rho1, upper.rho1)
  rho2 ~ dunif(lower.rho2, upper.rho2)
  rho3 ~ dunif(lower.rho3, upper.rho3)
  rho4 ~ dunif(lower.rho4, upper.rho4)
  rho5 ~ dunif(lower.rho5, upper.rho5)
  rho6 ~ dunif(lower.rho6, upper.rho6)
}
