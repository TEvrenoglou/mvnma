model {
  # k = number of studies
  # k2 = number of two-arm studies
  # n = number of treatments
    
  # This controls for studies with one outcome not reported by setting the
  # correlation equal to zero
  #
  for (i in 1:(2 * k - k2)) {
    control12[i] <- step(9999 - var1[i]) * step(9999 - var2[i])
    control13[i] <- step(9999 - var1[i]) * step(9999 - var3[i])
    control23[i] <- step(9999 - var2[i]) * step(9999 - var3[i])
  }
  
  # Two-arm studies
  #
  for (i in 1:k2) {
    #
    # Variances
    #
    S2[i, 1, 1] <- var1[i] + psi1.sq
    S2[i, 2, 2] <- var2[i] + psi2.sq
    S2[i, 3, 3] <- var3[i] + psi3.sq
    #
    # Covariances
    #
    S2[i, 1, 2] <- sqrt(S2[i, 1, 1]) * sqrt(S2[i, 2, 2]) * control12[i] * rho1
    S2[i, 1, 3] <- sqrt(S2[i, 1, 1]) * sqrt(S2[i, 3, 3]) * control13[i] * rho2
    S2[i, 2, 3] <- sqrt(S2[i, 2, 2]) * sqrt(S2[i, 3, 3]) * control23[i] * rho3
    #
    S2[i, 2, 1] <- S2[i, 1, 2]
    S2[i, 3, 1] <- S2[i, 1, 3]
    S2[i, 3, 2] <- S2[i, 2, 3]
    #
    # Estimates
    #
    y[(3 * i - 2):(3 * i)] ~ dmnorm.vcov(mean[(3 * i - 2):(3 * i)], S2[i, , ])
  }
  
  # Three-arm studies
  #
  for (i in 1:(k - k2)) {
    #
    # Variances
    #
    S3[i, 1, 1] <- var1[k2 + 2 * i - 1] + psi1.sq
    S3[i, 2, 2] <- var2[k2 + 2 * i - 1] + psi2.sq
    S3[i, 3, 3] <- var3[k2 + 2 * i - 1] + psi3.sq
    #
    S3[i, 4, 4] <- var1[k2 + 2 * i] + psi1.sq
    S3[i, 5, 5] <- var2[k2 + 2 * i] + psi2.sq
    S3[i, 6, 6] <- var3[k2 + 2 * i] + psi3.sq
    #
    # Covariances
    #
    S3[i, 1, 2] <- sqrt(S3[i, 1, 1]) * sqrt(S3[i, 2, 2]) * control12[i] * rho1
    S3[i, 1, 3] <- sqrt(S3[i, 1, 1]) * sqrt(S3[i, 3, 3]) * control13[i] * rho2
    S3[i, 2, 3] <- sqrt(S3[i, 2, 2]) * sqrt(S3[i, 3, 3]) * control23[i] * rho3
    #
    S3[i, 4, 5] <- sqrt(S3[i, 4, 4]) * sqrt(S3[i, 5, 5]) * control12[i] * rho1
    S3[i, 4, 6] <- sqrt(S3[i, 4, 4]) * sqrt(S3[i, 6, 6]) * control13[i] * rho2
    S3[i, 5, 6] <- sqrt(S3[i, 5, 5]) * sqrt(S3[i, 6, 6]) * control23[i] * rho3
    # Same outcome
    S3[i, 1, 4] <- 0.5 * sqrt(S3[i, 1, 1]) * sqrt(S3[i, 4, 4])
    S3[i, 2, 5] <- 0.5 * sqrt(S3[i, 2, 2]) * sqrt(S3[i, 5, 5])
    S3[i, 3, 6] <- 0.5 * sqrt(S3[i, 3, 3]) * sqrt(S3[i, 6, 6])
    #
    S3[i, 1, 5] <- 0.5 * sqrt(S3[i, 1, 1]) * sqrt(S3[i, 5, 5]) * control12[i] * rho1
    S3[i, 2, 4] <- 0.5 * sqrt(S3[i, 2, 2]) * sqrt(S3[i, 4, 4]) * control12[i] * rho1
    #
    S3[i, 1, 6] <- 0.5 * sqrt(S3[i, 1, 1]) * sqrt(S3[i, 6, 6]) * control13[i] * rho2
    S3[i, 3, 4] <- 0.5 * sqrt(S3[i, 3, 3]) * sqrt(S3[i, 4, 4]) * control13[i] * rho2
    #
    S3[i, 2, 6] <- 0.5 * sqrt(S3[i, 2, 2]) * sqrt(S3[i, 6, 6]) * control23[i] * rho3
    S3[i, 3, 5] <- 0.5 * sqrt(S3[i, 3, 3]) * sqrt(S3[i, 5, 5]) * control23[i] * rho3
    #
    S3[i, 2, 1] <- S3[i, 1, 2]
    S3[i, 3, 1] <- S3[i, 1, 3]
    S3[i, 4, 1] <- S3[i, 1, 4]
    S3[i, 5, 1] <- S3[i, 1, 5]
    S3[i, 6, 1] <- S3[i, 1, 6]
    S3[i, 3, 2] <- S3[i, 2, 3]
    S3[i, 4, 2] <- S3[i, 2, 4]
    S3[i, 5, 2] <- S3[i, 2, 5]
    S3[i, 6, 2] <- S3[i, 2, 6]
    S3[i, 4, 3] <- S3[i, 3, 4]
    S3[i, 5, 3] <- S3[i, 3, 5]
    S3[i, 6, 3] <- S3[i, 3, 6]
    S3[i, 5, 4] <- S3[i, 4, 5]
    S3[i, 6, 4] <- S3[i, 4, 6]
    S3[i, 6, 5] <- S3[i, 5, 6]
    #
    # Estimates
    #
    y[(3 * k2 + 6 * i - 5):(3 * k2 + 6 * i)] ~ dmnorm.vcov(mean[(3 * k2 + 6 * i - 5):(3 * k2 + 6 * i)], S3[i, , ])
  }
  
  # Parameterization of the means
  #
  for (i in 1:k2) {
    mean[3 * i - 2] <- d1[treat2[i]] - d1[treat1[i]]
    mean[3 * i - 1] <- d2[treat2[i]] - d2[treat1[i]]
    mean[3 * i    ] <- d3[treat2[i]] - d3[treat1[i]]
  }
  #
  for (i in 1:(k - k2)) {
    mean[3 * k2 + 6 * i - 5] <- d1[treat2[k2 + i]] - d1[treat1[k2 + i]]
    mean[3 * k2 + 6 * i - 4] <- d2[treat2[k2 + i]] - d2[treat1[k2 + i]]
    mean[3 * k2 + 6 * i - 3] <- d3[treat2[k2 + i]] - d3[treat1[k2 + i]]
    #
    mean[3 * k2 + 6 * i - 2] <- d1[treat3[k2 + i]] - d1[treat1[k2 + i]]
    mean[3 * k2 + 6 * i - 1] <- d2[treat3[k2 + i]] - d2[treat1[k2 + i]]
    mean[3 * k2 + 6 * i    ] <- d3[treat3[k2 + i]] - d3[treat1[k2 + i]]
  }
  
  # Priors
  #
  for (i in 1:(ref - 1)) {
    d1[i] ~ dnorm(0, 1e-03)
    d2[i] ~ dnorm(0, 1e-03)
    d3[i] ~ dnorm(0, 1e-03)
  }
  #
  d1[ref] <- 0
  d2[ref] <- 0
  d3[ref] <- 0
  #
  for (i in (ref + 1):n) {
    d1[i] ~ dnorm(0, 1e-03)
    d2[i] ~ dnorm(0, 1e-03)
    d3[i] ~ dnorm(0, 1e-03)
  }
  #
  psi1.sq <- psi1 * psi1
  psi2.sq <- psi2 * psi2
  psi3.sq <- psi3 * psi3
  #
  psi1 ~ dnorm(0, 1)T(0, )
  psi2 ~ dnorm(0, 1)T(0, )
  psi3 ~ dnorm(0, 1)T(0, )
  #
  rho1 ~ dunif(lower.rho1, upper.rho1)
  rho2 ~ dunif(lower.rho2, upper.rho2)
  rho3 ~ dunif(lower.rho3, upper.rho3)
}
