model {
  # k = number of studies
  # n = number of treatments
    
  # This controls for studies with one outcome not reported by setting the
  # correlation equal to zero
  #
  for (i in 1:k) {
    control12[i] <- step(9999 - var1[i]) * step(9999 - var2[i])
  }
  
  # Two-arm studies
  #
  for (i in 1:k) {
    #
    # Variances
    #
    S2[i, 1, 1] <- var1[i] + psi1.sq
    S2[i, 2, 2] <- var2[i] + psi2.sq
    #
    # Covariances
    #
    S2[i, 1, 2] <- sqrt(S2[i, 1, 1]) * sqrt(S2[i, 2, 2]) * control12[i] * rho1
    #
    S2[i, 2, 1] <- S2[i, 1, 2]
    #
    # Estimates
    #
    y[(2 * i - 1):(2 * i)] ~ dmnorm.vcov(mean[(2 * i - 1):(2 * i)], S2[i, , ])
  }
  
  # Parameterization of the means
  #
  for (i in 1:k) {
    mean[2 * i - 1] <- d1[treat2[i]] - d1[treat1[i]]
    mean[2 * i    ] <- d2[treat2[i]] - d2[treat1[i]]
  }
  
  # Priors
  #
  
 d1[ref] <- 0

 d2[ref] <- 0

for(i in 1:(ref-1)){
  
  for (m in 1:2){
    
    meand[m,i] <- alpha[i] + gamma[m]
    
    d[m,i] ~ dnorm(meand[m,i],prec.exp)
    
  }
  
  d1[i] <- d[1,i]
  
  d2[i] <- d[2,i]
   
}

for(i in (ref+1):n) {
  
  for (m in 1:2){
    
    meand[m,i] <- alpha[i] + gamma[m]
    
    d[m,i] ~ dnorm(meand[m,i],prec.exp)
    
  }
  
  d1[i] <- d[1,i]
  
  d2[i] <- d[2,i]
  
}

 for(m in 1:2) {
  
  gamma[m] ~ dnorm(0, 1e-03) 
  
 }


 for(i in 1:(ref-1)) {
  
  alpha[i] ~ dnorm(0,1e-03)
  
 }

 for(i in (ref+1):n) {
  
  alpha[i] ~ dnorm(0,1e-03)
  
 }
  #
  prec.exp <- 1/sigma.sq
  sigma.sq <- sigma * sigma
  sigma ~ dnorm(0,1)T(0,)
  #
  psi1.sq <- psi1 * psi1
  psi2.sq <- psi2 * psi2
  #
  psi1 ~ dnorm(0, prec.psi1)T(0, )
  psi2 ~ dnorm(0, prec.psi2)T(0, )
  #
  rho1 ~ dunif(lower.rho1, upper.rho1)
}
